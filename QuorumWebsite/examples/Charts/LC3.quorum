use Libraries.Compute.Statistics.DataFrame
use Libraries.Interface.Controls.Charts.LineChart

/*
    This is an example of a Line Chart built in quorum.
    The dataset we will be working with examining how much spending people in the US and the UK 
    have spent on Christmas over the years.
*/

// Create a dataframe to hold the data.
DataFrame frame

// Load your data file into the frame.
frame:Load("data/Christmas spending.csv")

// grabs the average spending values and place as y axis

// Select data from the frame that you wish to use in your line chart.
// Note: In Line Charts, non-numerical columns are not supported.
// For this example we will be comparing two numerical columns using their values as the y-axis.
frame:AddSelectedColumn(1) // spending values in US
frame:AddSelectedColumn(2) // spending values in UK

// Set the factor as "Year" which will represent the independent values on the x-axis
frame:AddSelectedFactor(0)

// Using the frame, create a LineChart object.
LineChart chart = frame:LineChart()

// Give the chart a descriptive title.
chart:SetTitle("Cost of Christmas Shopping")

// Add a subtitle for further description.
chart:SetSubtitle("Overtime, shopping costs have increased over the years in XMAS shopping")

// Give the x axis a descriptive title.
chart:SetXAxisTitle("Year")

// Give the y axis a descriptive title.
chart:SetYAxisTitle("Money Spent (in $)")

// Give the legend a descriptive title.
chart:SetLegendTitle("Christmas Shopping Spending")

// By default, different lines are created in separate sub charts.
// You can merged them into a single chart.
chart:SeparateBySeries(false)

// Set a custom interval along the x axis to increments of 5 for a better display.
chart:SetXTickInterval(5)

// You can adjust line and point density on your chart. 
chart:SetLineDensity(4)

// If needed, you can change the color palette to a predefined palette or create a custom one.
// Calm color palette contains pastels and neutral colors
chart:SetColorPaletteToCalm()

// Display your line chart.
chart:Display()
