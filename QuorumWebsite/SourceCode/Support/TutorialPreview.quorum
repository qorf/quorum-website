use Libraries.Web.Page.all
use Libraries.Containers.Array

class TutorialPreview is Division
    action Create(text title, Lesson lesson) returns TutorialPreview
        // Setting card attributes
        SetIdentifier("previewBar" + title)
        AddAttribute("data-test", "previewBar" + title)
        Lesson startLesson = lesson
        //make the max width the col 3 span, needs both flex and hidden to work
        AddClassAttribute("bg-neutral-grey-25 font-sans rounded-2xl box-border relative p-6 w-full gap-y-2 flex flex-col hidden sm:col-span-1 md:col-span-2 lg:col-span-3 xl:col-span-4 2xl:col-span-5 max-w-[384px] md:max-w-full")

        // Top heading
        Heading h2
        h2:SetScale(2)
        h2:AddText("Tutorial: " + title)
        h2:AddClassAttribute("text-2xl font-bold text-neutral-grey-90")
        Add(h2)

        Division content
        content:AddClassAttribute("flex flex-col gap-y-3")

        integer i = 0
        Division div_guiding_question
        Heading h_guiding_question = div_guiding_question:AddHeading("What will I learn?", 3)
        h_guiding_question:AddClassAttribute("text-lg font-bold")

        div_guiding_question:AddClassAttribute("flex flex-col gap-y-2")
        lesson = startLesson
        UnorderedList ul
        ul:AddClassAttribute("list-disc list-outside pl-8")
        repeat while lesson not= undefined
          ListItem li
          li:AddText(lesson:GetShortDescription())
          li:AddClassAttribute("text-sm")
          ul:Add(li)
          lesson = lesson:GetNextLesson()
          i = i + 1
        end
        div_guiding_question:Add(ul)

        content:Add(div_guiding_question)

        i = 0
        Division lessonDiv
        lessonDiv:AddClassAttribute("flex flex-col gap-y-2")
        lesson = startLesson
        repeat while lesson not= undefined
          Division div_link
          div_link:AddClassAttribute("flex flex-row gap-x-2 items-center")
          //creating a div that is a circle with a number in it
          Division circle
          circle:AddClassAttribute("bg-neutral-white rounded-full w-8 h-8 flex items-center justify-center")
          //adding the number to the circle
          Span index
          index:AddText((i + 1) + "")
          index:AddClassAttribute("text-neutral-black font-bold")
          circle:Add(index)
          div_link:Add(circle)

          Link link
          link:AddText(lesson:GetLinkName())
          link:SetAddress(lesson:GetLocation())
          link:AddClassAttribute("font-bold hover:underline")
          div_link:Add(link)
          lessonDiv:Add(div_link)
          lesson = lesson:GetNextLesson()
          i = i + 1
        end

        content:Add(lessonDiv)

        Add(content)

        return me
    end
end
